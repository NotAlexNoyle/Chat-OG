plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.8.0'
    id "com.github.johnrengelman.shadow" version "8.1.1"
}

group = 'nl.skbotnl.chat-og'
version = '1.1'

def apiVersion = "1.18.2"

processResources {
    def props = [
        version: version,
        apiVersion: apiVersion,
    ]

    inputs.properties props // Indicates to rerun if version changes.

    filesMatching("plugin.yml") { // Pass build number and API version to plugin's YAML file.
        expand props // Automatically update .jar's internal version numbers.
    }
}

repositories {
    mavenCentral()

    maven { // Import Maven Repository.
        url "https://repo.purpurmc.org/snapshots" // Get Purpur API from Purpur Maven Repository.
    }

    maven {
        url 'https://jitpack.io'
    }

    maven {
        url "https://repo.extendedclip.com/content/repositories/placeholderapi/"
    }
}

dependencies {
    compileOnly "org.purpurmc.purpur:purpur-api:1.18.2-R0.1-SNAPSHOT"
    compileOnly "com.github.MilkBowl:VaultAPI:1.7.1"
    compileOnly "me.clip:placeholderapi:2.11.3"

    implementation("net.dv8tion:JDA:5.0.0-beta.8") {
        exclude module: 'opus-java'
    }
    implementation "com.github.minndevelopment:jda-ktx:9fc90f616b7c9b68b8680c7bf37d6af361bb0fbb"
    implementation "com.github.SKBotNL:discord-webhooks:0.8.3"

    implementation "com.google.code.gson:gson:2.10.1"
}

shadowJar {
    minimize()
}

jar.dependsOn shadowJar

tasks.jar.configure {
    archiveClassifier = "part"
}

tasks.shadowJar.configure {
    archiveClassifier = null
}

kotlin {
    jvmToolchain(17)
}